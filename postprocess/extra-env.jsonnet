/**
 * Adjust StatefulSet generated by helm template
 */
local com = import 'lib/commodore.libjsonnet';
local inv = com.inventory();
local params = inv.parameters.keycloak;

local statefulset_file = std.extVar('output_path') + '/statefulset.yaml';
local statefulset = com.yaml_load(statefulset_file);

local insertMap(sup, map) = std.set(
  [
    {
      name: k,
    } + com.makeMergeable(map[k])
    for k in std.objectFields(map)
  ]
  +
  sup, function(c) c.name
);

{
  statefulset: statefulset {
    spec+: {
      template+: {
        spec+: {
          containers: [
            if c.name == 'keycloak' then
              c {
                env: insertMap(
                  if 'env' in super then super.env else [],
                  params.extraEnv
                ),
                volumeMounts: insertMap(
                  if 'volumeMounts' in super then super.volumeMounts else [],
                  params.extraVolumeMounts
                ),
              }
            else
              c
            for c in super.containers
          ],
          initContainers: insertMap(
            if 'initContainers' in super then super.initContainers else [],
            params.extraInitContainers
          ),
          volumes: insertMap(
            if 'volumes' in super then super.volumes else [],
            params.extraVolumes
          ),
        },
      },
    },
  },
}
