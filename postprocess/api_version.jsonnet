/**
 * Adjust StatefuleSet generated by helm template:
 * * Fix the apiVersion
 */
local com = import 'lib/commodore.libjsonnet';
local kap = import 'lib/kapitan.libjsonnet';
local inv = kap.inventory();
local params = inv.parameters.keycloak;

local chart_output_dir = std.extVar('output_path');

local list_dir(dir, basename=true) =
  std.native('list_dir')(dir, basename);

local chart_files = list_dir(chart_output_dir);

local input_file(elem) = chart_output_dir + '/' + elem;
local stem(elem) =
  local elems = std.split(elem, '.');
  std.join('.', elems[:std.length(elems) - 1]);


local fix_api_version(sts) =
  sts {
    apiVersion: 'apps/v1',
  };

local fixup_obj(obj) =
  if obj.kind == 'StatefulSet' then
    fix_api_version(obj)
  else
    obj;

local fixup(obj_file) =
  local objs = std.prune(com.yaml_load_all(obj_file));
  // process all objs
  [ fixup_obj(obj) for obj in objs ];

{
  [stem(elem)]: fixup(input_file(elem))
  for elem in chart_files
}
